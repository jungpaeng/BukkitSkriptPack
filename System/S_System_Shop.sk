options:
  ShopPrefixDirect: /plugins/Skript/scripts/Shop/
  ShopMenuTxt: /plugins/Skript/scripts/Shop/shop-menu.txt

  ShopPrefix: &6[&f상점&6]&r
  ShopMenuPrefix: <green>[Shop-Munu]<black> 메뉴 선택
  ShopItemPrefix: <green>[Shop-Item]<black>
  CODE: playerData

  ButtonGoMainPage: Redstone Dust named "<light red><bold>메인 메뉴" with lore "" and "<green>[좌클릭] <pink>메인 메뉴로 이동합니다."

on load:
  if file "{@ShopMenuTxt}" exists:
    # 상점 파일의 내용을 변수로 갱신
    set {S.Menu::*} to file contents of "{@ShopMenuTxt}"
    loop {S.Menu::*}:
      set {S.Menu::%loop-index%.Parse::*} to loop-value split by "_"
      set {S.Menu::%loop-index%.info.name} to "<white><bold>%{S.Menu::%loop-index%.Parse::2}% 열기"
      set {S.Menu::%loop-index%.info.lore::*} to "" and "<green>[좌클릭] <pink>%{S.Menu::%loop-index%.Parse::2}%을 엽니다."

      set {_ShopItemsDirect} to "{@ShopPrefixDirect}%{S.Menu::%loop-index%.Parse::1}%"
      if file "%{_ShopItemsDirect}%" exists:
        set {S.Menu::%loop-index%.Items::*} to file contents of "%{_ShopItemsDirect}%"
        loop {S.Menu::%loop-index%.Items::*}:
          set {S.Menu::%loop-index-1%.Items::%loop-index-2%.Parse::*} to loop-value-2 split by "_"
  # 콘솔에 로드 완료되었다는 문구 출력
  send "[%script%] Successfully loaded." to console

on unload:
  # 상점 관련 정보 제거
  delete {S.Menu::*}

command /shop:
  trigger:
    if {S.Menu::*} is set:
      open "CHEST" with 6 rows named "{@ShopMenuPrefix}" to player

      loop {S.Menu::*}:
        set slot {S.Menu::%loop-index%.Parse::3} parsed as number of current inventory of player to {S.Menu::%loop-index%.Parse::4} parsed as itemtype named {S.Menu::%loop-index%.info.name} with lore {S.Menu::%loop-index%.info.lore::*}
    else:
      send "상점 파일이 존재하지 않습니다. 관리자에게 문의해주세요."

# 상점 메뉴 부분
on player inventory click:
  if inventory name is "{@ShopMenuPrefix}":
    cancel event
    loop {S.Menu::*}:
      if {S.Menu::%loop-index%.info.name} contain name of clicked item:
        close player's inventory

        set {S.Menu.%uuid of player%.Select} to loop-index
        open "CHEST" with 6 rows named "{@ShopItemPrefix} %{S.Menu::%loop-index%.Parse::2}%" to player
        if {S.Menu::%loop-index%.Items::*} is set:
          loop {S.Menu::%loop-index%.Items::*}:
            set slot {S.Menu::%loop-index-1%.Items::%loop-index-2%.Parse::1} parsed as number of current inventory of player to {S.Menu::%loop-index-1%.Items::%loop-index-2%.Parse::2} parsed as itemtype with lore "&a[ &f구매가격 &a] &f%{S.Menu::%loop-index-1%.Items::%loop-index-2%.Parse::4}%" and "&a[ &f판매가격 &a] &f%{S.Menu::%loop-index-1%.Items::%loop-index-2%.Parse::5}%", "", "&d< &f이용 방법 &d>","&e&l└ &f좌클릭 시 구매 | &f우클릭 시 판매","&e&l- &fSHIFT + 좌클릭 | 64개구매","&e&l- &fSHIFT + 우클릭 | 모두판매"  
          set slot 49 of current inventory of player to {@ButtonGoMainPage}
        else:
          send "상점 서브 파일이 존재하지 않습니다. 관리자에게 문의해주세요."

# 서브 상점 메뉴일 때 이벤트
on player inventory click:
  if inventory name contains "{@ShopItemPrefix}":
    cancel event
    
    loop {S.Menu::%{S.Menu.%uuid of player%.Select}%.Items::*}:
      set {_currentItemParsed::*} to {S.Menu::%{S.Menu.%uuid of player%.Select}%.Items::%loop-index%.Parse::*}
      set {_controlBlock} to {_currentItemParsed::2} parsed as itemtype

      if clicked item is {_controlBlock}:
        if "%click type%" contains "LEFT":
          if "%click type%" contains "SHIFT":   # SHIFT - LEFT
            set {_controlBlock.count} to 64
          else:                                 # LEFT
            set {_controlBlock.count} to 1
          set {_controlBlock.amount} to {_currentItemParsed::4} parsed as number * {_controlBlock.count}

          if {data.%uuid of player%.{@CODE}::money} >= {_controlBlock.amount}:
            remove {_controlBlock.amount} from {data.%uuid of player%.{@CODE}::money}
            give {_controlBlock.count} of {_controlBlock} to player
            send "{@ShopPrefix} &a%{_currentItemParsed::2}% &6%{_controlBlock.count}%&6개&a를 &6%{_controlBlock.amount}%&6원&a에 구입하셨습니다."
          else:
            send "{@ShopPrefix} <light red>소지한 금액이 <light green>%{_controlBlock.amount}%<light green>원<light red>보다 적습니다."
        else if "%click type%" contains "RIGHT":
          if number of {_controlBlock} in the player's inventory >= 1:
            if "%click type%" contains "SHIFT":   # SHIFT - RIGHT
              set {_controlBlock.count} to number of {_controlBlock} in the player's inventory
              set {_controlBlock.amount} to {_currentItemParsed::5} parsed as number * {_controlBlock.count}

              loop all items in player's inventory:
                if loop-item is {_controlBlock}:
                  remove 64 of loop-item from player
            else:                                 # RIGHT
              loop all items in player's inventory:
                send "%loop-item%" to console
                if loop-item is {_controlBlock}:
                  if number of loop-item in the player's inventory >= 64:
                    set {_controlBlock.count} to 64
                  else:
                    set {_controlBlock.count} to 1
                  set {_controlBlock.amount} to {_currentItemParsed::5} parsed as number * {_controlBlock.count}
                  remove {_controlBlock.count} of loop-item from player
                  exit loop
            add {_controlBlock.amount} to {data.%uuid of player%.{@CODE}::money}
            send "{@ShopPrefix} &a%{_currentItemParsed::2}% &6%{_controlBlock.count}%개&a를 &6%{_controlBlock.amount}%&6원&a에 판매하셨습니다."
          else:
            send "{@ShopPrefix} &c'%{_currentItemParsed::2}%&c'이 부족합니다."
        stop

    if clicked item is {@ButtonGoMainPage}:
      close player's inventory
      make player execute command "/shop"


on inventory close:
  if {S.Menu.%uuid of player%.Select} is set:
    delete {S.Menu.%uuid of player%.Select}
