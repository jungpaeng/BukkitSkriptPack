on load:
  send "[%script%] Successfully loaded." to console

function saveYamlToValue(pu: text, code: text):
  # _dir 변수에 데이터 위치 불러오기
  if {_pu} is "server":
    set {_dir} to "%{dbDirect::%{_code}%}%.yaml"
  else:
    set {_dir} to "%{dbDirect::%{_code}%}%/%{_pu}%.yaml"

  # DB에 있는 PlayerInfo 데이터를 Value에 불러오기
  if file {_dir} exists:
    set {_nodes::*} to yaml nodes "" from {_dir}
    while number of {_nodes::*} is not 0:
      loop {_nodes::*}:
        set {_value} to yaml value loop-value-1 from {_dir}
        if "%{_value}%" don't contain "MemorySection":
          set {data.%{_pu}%.%{_code}%::%loop-value-1%} to {_value}
        else:
          loop yaml nodes loop-value-1 from {_dir}:
            add "%loop-value-1%.%loop-value-2%" to {_nodes::*}
        remove loop-value-1 from {_nodes::*}

function saveValueToYaml(pu: text, code: text):
  # _dir 변수에 데이터 위치 불러오기
  if {_pu} is "server":
    set {_dir} to "%{dbDirect::%{_code}%}%.yaml"
  else:
    set {_dir} to "%{dbDirect::%{_code}%}%/%{_pu}%.yaml"
  
  # 데이터가 존재하는 경우
  if {data.%{_pu}%.%{_code}%::*} is set:
    # 기존 yaml 제거
    loop yaml nodes "" from {_dir}:
      delete yaml nodes "%loop-value%" from {_dir}
    # 데이터 내보내기 및 value 데이터 제거
    loop {data.%{_pu}%.%{_code}%::*}:
      set yaml value loop-index from {_dir} to loop-value
      delete {data.%{_pu}%.%{_code}%::%loop-index%}

function getTotalExperience(xp: number) :: number:
  if {_xp} < 17:
    set {_totalExperience} to {_xp} * {_xp} + 6 * {_xp}
  else if {_xp} < 32:
    set {_totalExperience} to 2.5 * {_xp} * {_xp} - 40.5 * {_xp} + 360
  else:
    set {_totalExperience} to 4.5 * {_xp} * {_xp} - 162.5 * {_xp} + 2220
  return {_totalExperience}

# 타이틀
function runTitle(p: player, t: text, s: text, 1: integer = 10, 2: integer = 20, 3: integer = 10):
  make console execute command "/title %{_p}% times %{_1}% %{_2}% %{_3}%"
  make console execute command "/title %{_p}% title {""text"":""%{_t}%""}"
  make console execute command "/title %{_p}% subtitle {""text"":""%{_s}%""}"

# 액션바
function runActionbar(p:player,t:text):
  make console execute command "/title %{_p}% actionbar {""text"":""%{_t}%""}"

function getMoneyUnit(money: number) :: text:
  set {_r} to "%{_money}%"

  loop round up length of {_r} / 3 times:
    set {_l} to the last 3 characters of {_r}
    set {_r} to the first length of {_r} - 3 characters of {_r}
    if loop-number is 1:
      set {_t} to "%{_l}%"
    else:
      set {_t} to "%{_l}%,%{_t}%"

  return {_t}